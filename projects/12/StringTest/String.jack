// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array word;
    field int length;
    field int maxLength;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int max_length) {
        if(max_length = 0){
            let max_length = 1;
        }
        let maxLength = max_length;
        let word = Array.new(max_length);
        let length = 0;

        return this;
    }   

    /** Disposes this string. */
    method void dispose() {
        do Array.dispose(word);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return word[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let word[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if(length < maxLength){
            let word[length] = c;
            let length = length + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        let word[length] = 0;
        let length = length - 1;
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int total;
        var int count;
        var int isNegative;

        let count = 0;
        if(word[0] = 45){
            let isNegative = 1;
            let count = 1;
        }

        while(count < length){
            let total = total * 10 + (word[count] - 48);
            let count = count + 1;
        }

        if(isNegative){
            let total = -total;
        }

        return total;
    }

    // returns the string representation of a non negative integer
    function String int2String(int val){
        var int lastDigit;
        var String c; // character representing last digit
        var String rValue;

        let lastDigit = val - ((val / 10) * 10);

        let c = String.new(16);
        do c.appendChar(lastDigit + 48);

        if(val < 10){
            return c;
        }
        else {
            let rValue = String.int2String(val / 10);
            do rValue.appendChar(lastDigit + 48);
            return rValue;
        }
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        var int i;
        var String intWord;
        var int isNegative;

        let isNegative = 0;
        if(val < 0){
            let isNegative = 1;
            let val = val * -1;
        }

        let intWord = String.int2String(val);
        let i = 0;

        if(isNegative){
            let word[0] = 45;
            let i = 1;
        }

        while(i < (intWord.length() + isNegative)){
            let word[i] = intWord.charAt(i - isNegative);
            let i = i + 1;
        }
        let length = i;

        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
