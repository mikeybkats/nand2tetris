/** Runs the black jack game

- Deck class - creates the cards
- Deck class - shuffles the deck
- BlackJack class prompts for player to start the game
- Dealer class - deals cards
- Score class - keeps score for player and dealer
- when a player goes over 21 they lose the hand
- when a player loses the game prompts the player if they want to play again.
- the dealer stands when their score is 17 or higher

class BlackJack
    field gameActive: boolean // 0 or 1
    field winner: "player" | "dealer"
    field deck: Deck
    field turn: "player" = 0 | "dealer" = 1 

    field playerCards: Array<Card>
    field dealerCards: Array<Card>
    field playerScore: int
    field dealerScore: int

    field promptString: String
    field prompt: Prompt

    method gameLoop: void
        * show title screen
        * start game - set gameActive to true
        * while gameActive:
            * dealer deals cards. first dealer card remains hidden
            * calculate score
            * prompt player: would you like another card?
            * calculate score
            * if black jack player wins
            * if over 21 player bust
            * if player stays dealer flips hidden card
            * deals card while < 17
    
    method promptPlayerTitle: void
    method promptPlayerStart: void
    method loadDeck: void
    method deal: void
    method promptPlayerForAnotherCard: void
    method drawCard: void
    method checkForWinners: void
    method dealerPlay: void
    method revealCard: void
    method promptPlayerWinner: void
    method promptPlayerLoser: void

*/

class BlackJack {
    field Prompt prompt;
    field String promptString;
    field int gameActive;
    field Deck deck;
    field Score score;

    field Array playerCards;
    field Array dealerCards;

    field int randomizer;

    field Boolean hitPlayer;

    constructor BlackJack new() {
        let prompt = Prompt.new();
        let gameActive = 0;
        let playerCards = Array.new(9);
        let dealerCards = Array.new(9);
        let score = Score.new(0,0);
        let hitPlayer = true;
        let deck = Deck.new();

        return this;
    }

    method void start(){
        do promptPlayerStart("Welcome to Black Jack. Press spacebar to start.", 32, 0, 0);
        return;
    }

    method void testBlackJack(){
        //////// for testing
        var Suit cardSuitObj;
        var String cardSuit;
        var CardValue cardVal;
        let cardSuitObj = Suit.new();
        let cardSuit = cardSuitObj.getSpades();
        let cardVal = CardValue.new();
        let playerCards[0] = Card.new(cardSuit, cardVal.getAce(), cardSuitObj, cardVal);
        let playerCards[1] = Card.new(cardSuit, cardVal.getNine(), cardSuitObj, cardVal);
        let playerCards[2] = Card.new(cardSuit, cardVal.getAce(), cardSuitObj, cardVal);
        return;
    }

    method PromptData promptPlayerStart(String message, int keyCode, int escKeyCode, int shiftX) {

        var PromptData userInput;
        do prompt.setMessage(message);
        do prompt.setOffsetY(10);
        do prompt.shiftOffset(shiftX, 0);

        let userInput = prompt.promptUser();
        let randomizer = userInput.getElapsedTime();
        do Sys.wait(250);

        while(~(userInput.getKeyCode() = keyCode) & ~(userInput.getKeyCode() = escKeyCode)){
            let userInput = prompt.promptUser();
        }

        do prompt.setOffsetX(10);

        if (userInput.getKeyCode() = keyCode) {
            do prompt.eraseScreen();
            let gameActive = 1;

            // first deal cards to player
            // do testBlackJack();
            do dealCard(playerCards, randomizer + 1);
            do dealCard(playerCards, randomizer + 2);
            do dealCard(dealerCards, randomizer + 3);
            do dealCard(dealerCards, randomizer + 4);

            do gameLoop();


            do playAgain();
        } 

        return userInput;
    }

    method Boolean promptPlayerForAnotherCard() {
        var PromptData userInput;
        var int userInputKeyCode;
        let userInputKeyCode = 0;

        do prompt.shiftOffset(2, 0);
        do prompt.setMessage("Would you like to draw another card? (y/n)");
        let userInput = prompt.promptUser();
        
        let userInputKeyCode = userInput.getKeyCode();
        do Sys.wait(300);

        if (userInputKeyCode = 89){
            do prompt.shiftOffset(-2, 0);
            do dealCard(playerCards, userInput.getElapsedTime());

            return true;
        } else {
            do Sys.wait(250);
        }

        do prompt.shiftOffset(-2, 0);
        return false;
    }

    method void loadNewDeck(){
        let deck = Deck.new();
        return;
    }

    method void dealCard(Array dest, int randomizer){
        var Card cardFromDeck;
        var int i;

        let cardFromDeck = deck.getRandomCard(randomizer);
        let i = 0;

        while(~(dest[i] = null)){
            let i = i + 1;
        }

        let dest[i] = cardFromDeck;
        return;
    }

    method void printCard(Card card, int positionY, int offsetX, int shift){
        do card.setLocationY(positionY);
        do card.setLocationX((shift * 4) + 6 + offsetX);
        do card.render();
        return;
    }

    method void printHand(Array cards, int positionY, int offsetX) { 
        var Card card;
        var int i;
        let i = 0;

        while(~(cards[i] = null)) {
            do printCard(cards[i], positionY, offsetX, i);
            let i = i + 1;
        }

        return;
    }

    method void dealFirstHand(){
        do prompt.shiftOffset(14, 0);
        do prompt.setMessage("Dealing cards...");
        do prompt.printMessage();
        do prompt.shiftOffset(-14, 0);

        do Sys.wait(1000);
        do printCard(playerCards[0], 200, 5, 0);
        do Sys.wait(1000);
        do printCard(dealerCards[0], 10, 5, 0);

        do Sys.wait(1000);
        do printCard(playerCards[1], 200, 5, 1);
        do Sys.wait(1000);
        do printCard(dealerCards[1], 10, 5, 1);

        do Sys.wait(1000);
        return;
    }

    method void resetGame(){
        do deck.dispose();
        do playerCards.dispose();
        do dealerCards.dispose();
        do score.dispose();
        do prompt.setOffsetX(8);
        do prompt.setOffsetY(6);

        let deck = Deck.new();
        let playerCards = Array.new(9);
        let dealerCards = Array.new(9);
        let score = Score.new(0,0);
        let hitPlayer = true;

        return;
    }

    method void endGame(){
        do prompt.eraseScreen();
        do prompt.shiftOffset(10, -2);
        do prompt.setMessage("Thank you for playing!");
        do prompt.printMessage();

        do prompt.shiftOffset(-5, 2);
        do prompt.setMessage("Created by Michael Barakat, 2022");
        do prompt.printMessage();
        return;
    }

    method void playAgain(){
        var PromptData startGame;
        
        do resetGame();

        let startGame = promptPlayerStart("Would you like to play again? (y/n)", 89, 78, 6);

        if(startGame.getKeyCode() = 78){
            do endGame();
        }
        return;
    }

    method Boolean isRoyalCard(Card card){
        var CardValue cardValObj;
        var String cardName;

        let cardValObj = card.getCardValueObj();
        let cardName = card.getName();

        if(cardName = cardValObj.getJack()){
            return true;
        }
        if(cardName = cardValObj.getQueen()){
            return true;
        }
        if(cardName = cardValObj.getKing()){
            return true;
        }

        return false;
    }

    method Boolean isAce(Card card){
        var CardValue cardValObj;
        let cardValObj = card.getCardValueObj();

        if(card.getName() = cardValObj.getAce()){
            return true;
        }

        return false;
    }

    method Boolean isBlackJack(Array hand){
        if( (isAce(hand[0]) & isRoyalCard(hand[1])) | (isAce(hand[1]) & isRoyalCard(hand[0])) ){
            do prompt.setMessage("Black Jack, Good job.");
            do prompt.printMessage();
            do prompt.shiftOffset(0, 2);
            do prompt.setMessage("You win.");
            do prompt.printMessage();
            let gameActive = 0;

            return true;
        }
        return false;
    }

    method void dealerDealsTheirHand(int dealerCount){
        do dealCard(dealerCards, 3 + dealerCount);
        do score.setDealer(dealerCards);
        do score.print();
        do printHand(dealerCards, 10, 5);
        do Sys.wait(2000);
        return;
    }

    method void gameLoop(){
        var int gameCount;
        var int dealerCount;

        do dealFirstHand();

        while(gameActive = 1){
            // printCards
            do printHand(playerCards, 200, 5);
            do printHand(dealerCards, 10, 5);

            //  print score
            do score.setPlayer(playerCards);
            do score.setDealer(dealerCards);
            do score.print();

            // if over 21 player bust
            if(score.getPlayerScore() > 21){
                do prompt.shiftOffset(9,0);
                do prompt.setMessage("Busted. You have lost.");
                do prompt.printMessage();
                do prompt.shiftOffset(-9, 0);
                let gameActive = 0;
                let hitPlayer = false;

                do Sys.wait(2000);

                return;
            }

            // prompt player: would you like another card?
            if((hitPlayer = true) & (score.getPlayerScore() < 21)){
                let hitPlayer = promptPlayerForAnotherCard();
            }
            else {
                // turn the loop off if player is done drawing cards
                let gameActive = 0;

                if(score.getPlayerScore() = 21){
                    // if black jack player always wins
                    if(isBlackJack(playerCards) = true){
                        let gameActive = 0;
                        do Sys.wait(2000);
                        return;
                    }

                    do prompt.shiftOffset(12, 0);
                    do prompt.setMessage("21, Good job.");
                    do prompt.printMessage();
                    do prompt.shiftOffset(-12, 0);
                    do Sys.wait(2000);
                }

                // dealer draws cards if their score is under 18
                while((score.getDealerScore() < 18) | ( (score.getPlayerScore() = 21) & (score.getDealerScore() < 22) ) ){
                    do prompt.shiftOffset(12, 0);
                    do prompt.setMessage("Dealer draws...");
                    do prompt.printMessage();
                    do dealerDealsTheirHand(dealerCount);
                    do prompt.shiftOffset(-8, 0);
                    let dealerCount = dealerCount + 1;
                }

                if(score.getDealerScore() > 21){
                    do prompt.shiftOffset(8, 0);
                    do prompt.setMessage("Dealer busts! You win!");
                    do prompt.printMessage();
                    do prompt.shiftOffset(-8,0);
                    let gameActive = 0;

                    do Sys.wait(2000);
                    return;
                } 
                if((score.getPlayerScore() > score.getDealerScore()) & ~(score.getPlayerScore() > 21)){
                    do prompt.shiftOffset(8, 0);
                    do prompt.setMessage("You have beat the dealer.");
                    do prompt.printMessage();
                    do prompt.shiftOffset(8, 2);
                    do prompt.setMessage("You win.");
                    do prompt.printMessage();
                    do prompt.shiftOffset(-8, 2);
                    let gameActive = 0;

                    do Sys.wait(4000);
                    return;
                }

                if(score.getPlayerScore() = score.getDealerScore()){
                    do prompt.shiftOffset(8, 0);
                    do prompt.setMessage("It's a tie.");
                    do prompt.printMessage();
                    do prompt.shiftOffset(-8, 0);
                    let gameActive = 0;

                    do Sys.wait(2000);
                    return;
                }
                else {
                    do prompt.shiftOffset(8, 0);
                    do prompt.setMessage("The Dealer wins.");
                    do prompt.printMessage();
                    do prompt.shiftOffset(6, 2);
                    do prompt.setMessage("You lose.");
                    do prompt.shiftOffset(-8, 0);
                    do prompt.printMessage();
                    let gameActive = 0;

                    do Sys.wait(2000);
                    return;
                }
            }
        }
        return;
    }
}