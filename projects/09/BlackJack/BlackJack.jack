/** Runs the black jack game

- Deck class - creates the cards
- Deck class - shuffles the deck
- BlackJack class prompts for player to start the game
- Dealer class - deals cards
- Score class - keeps score for player and dealer
- when a player goes over 21 they lose the hand
- when a player loses the game prompts the player if they want to play again.
- the dealer stands when their score is 17 or higher

class BlackJack
    field gameActive: boolean // 0 or 1
    field winner: "player" | "dealer"
    field deck: Deck
    field turn: "player" = 0 | "dealer" = 1 

    field playerCards: Array<Card>
    field dealerCards: Array<Card>
    field playerScore: int
    field dealerScore: int

    field promptString: String
    field prompt: Prompt

    method gameLoop: void
        * show title screen
        * start game - set gameActive to true
        * while gameActive:
            * dealer deals cards. first dealer card remains hidden
            * calculate score
            * prompt player: would you like another card?
            * calculate score
            * if black jack player wins
            * if over 21 player bust
            * if player stays dealer flips hidden card
            * deals card while < 17
    
    method promptPlayerTitle: void
    method promptPlayerStart: void
    method loadDeck: void
    method deal: void
    method promptPlayerForAnotherCard: void
    method drawCard: void
    method checkForWinners: void
    method dealerPlay: void
    method revealCard: void
    method promptPlayerWinner: void
    method promptPlayerLoser: void

*/

class BlackJack {
    field Prompt prompt;
    field String promptString;
    field int gameActive;
    field Deck deck;
    field Score score;

    field Array playerCards;
    field Array dealerCards;

    field int randomizer;

    field Boolean hitPlayer;

    constructor BlackJack new() {
        let prompt = Prompt.new();
        let gameActive = 0;
        let playerCards = Array.new(9);
        let dealerCards = Array.new(9);
        let score = Score.new(0,0);
        let hitPlayer = true;
        let deck = Deck.new();

        return this;
    }

    method void start(){
        do promptPlayerStart("Welcome to Black Jack. Press spacebar to start.", 32, 0);
        return;
    }

    method PromptData promptPlayerStart(String message, int keyCode, int escKeyCode) {
        var PromptData userInput;
        do prompt.setMessage(message);
        do prompt.eraseScreen();

        let userInput = prompt.promptUser();
        let randomizer = userInput.getElapsedTime();
        do Sys.wait(250);

        while(~(userInput.getKeyCode() = keyCode) & ~(userInput.getKeyCode() = escKeyCode)){
            let userInput = prompt.promptUser();
        }

        if (userInput.getKeyCode() = keyCode) {
            let gameActive = 1;

            // first deal cards to player
            do dealCard(playerCards, randomizer);
            do dealCard(playerCards, randomizer + 3);
            do dealCard(dealerCards, randomizer + 4);
            do dealCard(dealerCards, randomizer + 2);

            do gameLoop();

            do playAgain();
        } 

        return userInput;
    }

    method Boolean promptPlayerForAnotherCard() {
        var PromptData userInput;
        var int userInputKeyCode;
        let userInputKeyCode = 0;

        do prompt.setMessage("Would you like to draw another card?");
        do prompt.printMessage();

        do prompt.shiftOffset(0, 1);
        do prompt.setMessage("press y for yes. press n for no.");
        let userInput = prompt.promptUser();
        do prompt.shiftOffset(0, -1);
        
        let userInputKeyCode = userInput.getKeyCode();
        do Sys.wait(300);

        if (userInputKeyCode = 89){
            do dealCard(playerCards, userInput.getElapsedTime());

            return true;
        } else {
            do Sys.wait(250);
        }

        return false;
    }

    method void loadNewDeck(){
        let deck = Deck.new();
        return;
    }

    method void dealCard(Array dest, int randomizer){
        var Card cardFromDeck;
        var int i;

        let cardFromDeck = deck.getRandomCard(randomizer);
        let i = 0;

        while(~(dest[i] = null)){
            let i = i + 1;
        }

        let dest[i] = cardFromDeck;
        return;
    }

    method void printHand(Array cards, int positionY, int offsetX) { 
        var Card card;
        var int i;
        let i = 0;

        while(~(cards[i] = null)) {
            let card = cards[i];
            do card.setLocationY(positionY);
            do card.setLocationX((i * 4) + 6 + offsetX);
            do card.render();
            let i = i + 1;
        }

        return;
    }

    method void gameLoop(){
        var int gameCount;
        var int dealerCount;

        while(gameActive = 1){
            // printCards
            do printHand(playerCards, 200, 5);
            do printHand(dealerCards, 10, 5);

            //  print score
            do score.setPlayer(playerCards);
            do score.setDealer(dealerCards);
            do score.print();

            // if over 21 player bust
            if(score.getPlayerScore() > 21){
                do prompt.setMessage("Busted. You have lost.");
                do prompt.printMessage();
                let gameActive = 0;
                let hitPlayer = false;

                do Sys.wait(3000);

                return;
            }

            // prompt player: would you like another card?
            if(hitPlayer = true){
                let hitPlayer = promptPlayerForAnotherCard();
            }
            else {
                // if black jack player wins
                if(score.getPlayerScore() = 21 & gameActive = 1){
                    do prompt.setMessage("Black Jack. Good job.");
                    do prompt.printMessage();
                    do prompt.shiftOffset(0, 2);
                    do prompt.setMessage("You win.");
                    do prompt.printMessage();
                    let gameActive = 0;

                    do Sys.wait(4000);
                    return;
                }
                // if no dealer draws cards
                while(score.getDealerScore() < 18){
                    do dealCard(dealerCards, 3 + dealerCount);
                    do printHand(dealerCards, 10, 5);
                    do Sys.wait(1000);

                    do score.setDealer(dealerCards);
                    do score.print();
                    let dealerCount = dealerCount + 1;
                }


                let gameActive = 0;

                if(score.getDealerScore() > 21){
                    do prompt.setMessage("Dealer busts! You win!");
                    do prompt.printMessage();
                    do Sys.wait(4000);
                    return;
                } 
                if((score.getPlayerScore() > score.getDealerScore()) & ~(score.getPlayerScore() > 21)){
                    do prompt.setMessage("You have beat the dealer.");
                    do prompt.printMessage();
                    do prompt.shiftOffset(0, 2);
                    do prompt.setMessage("You win.");
                    do prompt.printMessage();
                    let gameActive = 0;

                    do Sys.wait(4000);
                    return;
                }
                if(score.getPlayerScore() = score.getDealerScore()){
                    do prompt.setMessage("It's a tie.");
                    do prompt.printMessage();
                    let gameActive = 0;

                    do Sys.wait(4000);
                    return;
                }
                else {
                    do prompt.setMessage("The Dealer wins.");
                    do prompt.printMessage();
                    do prompt.shiftOffset(0, 2);
                    do prompt.setMessage("You lose.");
                    do prompt.printMessage();
                    let gameActive = 0;

                    do Sys.wait(4000);
                    return;
                }
            }
        }
        return;
    }

    method void resetGame(){
        do deck.dispose();
        do playerCards.dispose();
        do dealerCards.dispose();
        do score.dispose();
        do prompt.setOffsetX(8);
        do prompt.setOffsetY(6);

        let deck = Deck.new();
        let playerCards = Array.new(9);
        let dealerCards = Array.new(9);
        let score = Score.new(0,0);
        let hitPlayer = true;

        return;
    }

    method void endGame(){
        do prompt.eraseScreen();
        do prompt.shiftOffset(12, -2);
        do prompt.setMessage("Thank you for playing!");
        do prompt.printMessage();

        do prompt.shiftOffset(-4, 2);
        do prompt.setMessage("Created by Michael Barakat, 2022");
        do prompt.printMessage();
        return;
    }

    method void playAgain(){
        var PromptData startGame;
        
        do resetGame();

        do prompt.shiftOffset(6, 3);
        let startGame = promptPlayerStart("Would you like to play again? (y/n)", 89, 78);

        if(startGame.getKeyCode() = 78){
            do endGame();
        }
        return;
    }
}